plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'io.github.alexisTrejo11'
version = '1.0.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url = 'https://repo.spring.io/milestone' }
}

ext {
	set('springAiVersion', "1.0.0-M4")
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation 'org.mockito:mockito-junit-jupiter'

	// Database
	testImplementation 'com.h2database:h2'
	implementation 'org.springframework.ai:spring-ai-postgresml-spring-boot-starter'

	implementation 'commons-io:commons-io:2.11.0' //  file handling

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// auth
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'org.springframework.security:spring-security-oauth2-resource-server'
	implementation 'com.nimbusds:nimbus-jose-jwt:9.37.3'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	// Mail service
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// AutoMappers
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	implementation 'org.modelmapper:modelmapper:3.2.1'

	// Rate Limiter
	implementation 'com.bucket4j:bucket4j-core:8.10.1'
  implementation 'com.bucket4j:bucket4j-jcache:8.10.1'

	// cache
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.6'

	// Open Api
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

	// Migrations
	implementation 'org.flywaydb:flyway-core'
  implementation 'org.flywaydb:flyway-database-postgresql'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}


bootRun {
	jvmArgs = [
			"-XX:+TieredCompilation",
			"-XX:TieredStopAtLevel=1",
	]

	// Load .env file
	def envFile = file('.env')
	if (envFile.exists()) {
		envFile.readLines().each { line ->
			if (line && !line.startsWith('#') && line.contains('=')) {
				def (key, value) = line.split('=', 2)
				systemProperty key.trim(), value.trim()
				environment key.trim(), value.trim()
			}
		}
	}
}